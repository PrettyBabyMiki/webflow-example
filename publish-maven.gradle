apply plugin: "maven"

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect "optional" and "provided" dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = "provided"
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == "test"
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = "https://github.com/SpringSource/spring-webflow"
			organization {
				name = "SpringSource"
				url = "http://springsource.org/spring-webflow"
			}
			licenses {
				license {
					name "The Apache Software License, Version 2.0"
					url "http://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution "repo"
				}
			}
			scm {
				url = "https://github.com/SpringSource/spring-webflow"
				connection = "scm:git:git://github.com/SpringSource/spring-webflow"
				developerConnection = "scm:git:git://github.com/SpringSource/spring-webflow"
			}
			developers {
				developer {
					id = "rstoyanchev"
					name = "Rossen Stoyanchev"
					email = "rstoyanchev@vmware.com"
				}
				developer {
					id = "pwebb"
					name = "Phil Webb"
					email = "pwebb@vmware.com"
				}
			}
		}
	}
}
